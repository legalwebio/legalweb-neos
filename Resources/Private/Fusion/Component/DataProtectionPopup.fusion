prototype(LegalWeb.GdprTools:Component.DataProtectionPopup) < prototype(Neos.Fusion:Component) {
    dataProtectionPopup = ${LegalWeb.GdprTools.Services.getDataProtectionPopup(documentNode)}
    configJs = ${Array.join(Array.map(this.dataProtectionPopup.config, (value, key) => 'window.' + key + ' = ' + Json.stringify(value) + ';'), '')}

    style = ${this.dataProtectionPopup.css}
    // Add style for not close popup when clicking outside 
    additionalStyle = '.sp-dsgvo-popup-overlay {pointer-events: none !important;} .sp-dsgvo-privacy-popup.container-fluid.show {pointer-events: auto !important;}'
    html = ${this.dataProtectionPopup.html}
    javascript = ${'(function(){' + this.configJs + this.dataProtectionPopup.js + '})();'}
    
    renderer = afx`
        <style @if.set={props.style}>{props.style + props.additionalStyle}</style>
        {props.html}
        <script @if.set={props.javascript}>{props.javascript}</script>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            node = ${node}
            editPreviewMode = ${node.context.currentRenderingMode.name}
            customId = 'popup'
        }
        entryTags {
            1 = ${Neos.Caching.nodeTag(node)}
            2 = ${Neos.Caching.descendantOfTag(node)}
            3 = 'LegalWeb-DataProtectionPopup-Cache-EntryTag'
        }
    }
}
